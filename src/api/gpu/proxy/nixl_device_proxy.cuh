/*
 * SPDX-FileCopyrightText: Copyright (c) 2026 NVIDIA CORPORATION & AFFILIATES. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
#ifndef NIXL_SRC_API_GPU_PROXY_NIXL_DEVICE_PROXY_CUH
#define NIXL_SRC_API_GPU_PROXY_NIXL_DEVICE_PROXY_CUH

#include <cuda/atomic>
#include <cstring>

#include "../common/nixl_device_types.cuh"
#include "../../../core/device_proxy/proxy_protocol.h"

// Overlay struct written into nixlGpuXferStatusH::storage by enqueue()
// and read back by pollXferStatus().  Must fit within the 64-byte opaque blob.
struct ProxyXferStatus {
    nixlProxyCompletionSlot *slot;  // device pointer to the channel's nixlProxyCompletionSlot
    uint64_t        op_idx;
};
static_assert(sizeof(ProxyXferStatus) <= NIXL_GPU_XFER_STATUS_PAYLOAD_SIZE,
              "ProxyXferStatus must fit in the transfer-status payload");

__device__ __forceinline__ uint32_t
proxyMemViewIdFromHandle(nixlMemViewH mvh) {
    return mvh == nullptr ? 0 :
                            static_cast<const nixlProxyDeviceMemView *>(mvh)->proxy_memview_id;
}

static_assert(sizeof(*nixlProxyWorkRing{}.producer_idx) == 8,
              "producer_idx must be 64-bit to avoid wrap-around false completions");
static_assert(sizeof(*nixlProxyWorkRing{}.consumer_idx) == 8,
              "consumer_idx must be 64-bit to match producer_idx");
static_assert(sizeof(*nixlProxyWorkRing{}.consumer_idx_cache) == 8,
              "consumer_idx_cache must be 64-bit to match producer_idx");
static_assert(sizeof(nixlProxyCompletionSlot::completed_idx) == 8,
              "completed_idx must be 64-bit to match producer_idx");

template<nixl_gpu_level_t level>
__device__ inline void nixlProxyExecInit(uint32_t &lane_id) {
    static_assert(level != nixl_gpu_level_t::GRID,
                  "Proxy GPU backend does not support GRID-level operations");

    if constexpr (level == nixl_gpu_level_t::THREAD) {
        lane_id = 0;
    } else if constexpr (level == nixl_gpu_level_t::WARP) {
        lane_id = threadIdx.x % warpSize;
    } else if constexpr (level == nixl_gpu_level_t::BLOCK) {
        lane_id = threadIdx.x;
    }
}

template<nixl_gpu_level_t level>
__device__ inline void nixlProxySync() {
    static_assert(level != nixl_gpu_level_t::GRID,
                  "Proxy GPU backend does not support GRID-level operations");

    if constexpr (level == nixl_gpu_level_t::WARP) {
        __syncwarp();
    } else if constexpr (level == nixl_gpu_level_t::BLOCK) {
        __syncthreads();
    }
}

__device__ __forceinline__ size_t
nixlProxyChannelIndex(const nixlProxyDeviceContextData &context,
                      uint32_t peer_index,
                      uint32_t channel_id) {
    return static_cast<size_t>(channel_id) * context.max_peers + peer_index;
}

__device__ inline nixl_status_t
nixlProxyEnqueue(const nixlProxyDeviceContextData &context,
                 nixlProxySubmission submission,
                 nixlGpuXferStatusH *xfer_status = nullptr) {
    if (submission.dst_index >= context.max_peers || context.num_channels == 0 ||
        context.channels == nullptr || context.shutdown_word == nullptr) {
        return NIXL_ERR_INVALID_PARAM;
    }
    submission.channel_id = static_cast<uint16_t>(submission.channel_id % context.num_channels);

    cuda::atomic_ref<uint64_t, cuda::thread_scope_system> shut(*context.shutdown_word);
    if (shut.load(cuda::memory_order_relaxed) ==
        static_cast<uint64_t>(nixl_proxy_control_state_t::SHUTDOWN)) {
        return NIXL_ERR_BACKEND;
    }

    nixlProxyChannelView &channel_view =
        context.channels[nixlProxyChannelIndex(
            context, submission.dst_index, submission.channel_id)];
    if (channel_view.work_ring == nullptr || channel_view.completion_slot == nullptr) {
        return NIXL_ERR_REMOTE_DISCONNECT;
    }
    nixlProxyWorkRing *ring = channel_view.work_ring;

    cuda::atomic_ref<uint64_t, cuda::thread_scope_device> producer_idx(*ring->producer_idx);
    cuda::atomic_ref<uint64_t, cuda::thread_scope_system> cons(*ring->consumer_idx);
    const uint64_t ticket = producer_idx.fetch_add(1, cuda::memory_order_relaxed);

    uint64_t cached_consumer_idx = *ring->consumer_idx_cache;
    while (ticket - cached_consumer_idx >= ring->depth) {
        cached_consumer_idx = cons.load(cuda::memory_order_acquire);
        *ring->consumer_idx_cache = cached_consumer_idx;
        if (shut.load(cuda::memory_order_relaxed) ==
            static_cast<uint64_t>(nixl_proxy_control_state_t::SHUTDOWN)) {
            return NIXL_ERR_BACKEND;
        }
    }

    const uint64_t submission_op_idx = ticket + 1;
    const uint32_t slot = static_cast<uint32_t>(ticket % ring->depth);
    submission.op_idx = 0;
    ring->records[slot] = submission;

    cuda::atomic_ref<uint64_t, cuda::thread_scope_system> record_op_idx(
        ring->records[slot].op_idx);
    record_op_idx.store(submission_op_idx, cuda::memory_order_release);

    if (xfer_status != nullptr) {
        const ProxyXferStatus status{channel_view.completion_slot, submission_op_idx};
        memcpy(xfer_status->storage, &status, sizeof(status));
    }
    return NIXL_IN_PROG;
}

__device__ inline nixl_status_t
nixlProxyPollXferStatus(const nixlGpuXferStatusH &xfer_status) {
    const auto *status = reinterpret_cast<const ProxyXferStatus *>(xfer_status.storage);
    if (status->slot == nullptr) {
        return NIXL_ERR_BACKEND;
    }

    cuda::atomic_ref<uint64_t, cuda::thread_scope_system> completed_idx(
        status->slot->completed_idx);
    const uint64_t completed = completed_idx.load(cuda::memory_order_acquire);
    if (completed > status->op_idx) {
        return NIXL_SUCCESS;
    }
    const nixl_status_t current_status = status->slot->next_status;
    if (completed == status->op_idx) {
        return current_status;
    }
    return current_status < 0 ? current_status : NIXL_IN_PROG;
}

#endif // NIXL_SRC_API_GPU_PROXY_NIXL_DEVICE_PROXY_CUH
